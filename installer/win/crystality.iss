; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; #define VERSION "0.1.1" 
[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5ADAD22B-E0B0-41EC-9527-DB06F04AAAE5}
AppName=crystality
AppPublisher=crystality Dev Team
DefaultDirName={localappdata}\Crystality
DefaultGroupName=Crystality
OutputDir=.\..\..\bundle
OutputBaseFilename=Crystality-toolchain
SetupIconFile=assets\logo.ico
Compression=lzma
SolidCompression=yes
AppCopyright=Copyright (C) crystality Dev Team 2022
AllowUNCPath=False
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
UsePreviousPrivileges=False
MinVersion=6.1sp1
DisableProgramGroupPage=yes
PrivilegesRequired=lowest
UninstallDisplayIcon={app}\bin\crystality.exe
UninstallDisplayName=crystality Language Preview Toolchain
AppVersion={#VERSION}
VersionInfoVersion={#VERSION}
SetupLogging=yes
DisableWelcomePage=False
WizardSmallImageFile=assets\92_97.bmp
WizardImageFile=assets\273_556.bmp
SetupMutex=crystality_Dev

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: ".\..\..\bundle\Crystality\*"; DestDir: "{app}\"; Flags: ignoreversion createallsubdirs recursesubdirs
; Source: ".\..\..\bundle\crystality\scripts\*"; DestDir: "{app}\scripts"; Flags: ignoreversion createallsubdirs recursesubdirs

[Icons]

[Messages]


[Run]
Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\InstallExtension.ps1"" -ExtensionPath ""{app}\extensions\crystality.vsix"""; \
  WorkingDir: {app}; \
  Flags: runhidden

Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\ConfigureVSCode.ps1"" -BinPath ""{app}\bin\crystality.exe"""; \
  WorkingDir: {app}; \
  Flags: runhidden

Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\OpenContractFolder.ps1"" -Path ""{app}\examples"" -MDFile ""{app}\examples\AirDrop.xtlts"""; \
  WorkingDir: {app}; \
  Description: "Start VSCode and Open Folder of crystality code examples"; \
  Flags: postinstall nowait skipifsilent runhidden 

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

[Code]
function GetVSCodeInstallPath(): string;
var
  path: string;
begin

  // VSCODE64USER
  if RegQueryStringValue(HKEY_CURRENT_USER, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{771FD6B0-FA20-440A-A002-3B3BAC16DC50}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

  // VSCODE32USER
  if RegQueryStringValue(HKEY_CURRENT_USER, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{D628A17A-9713-46BF-8D57-E671B46A741E}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

  // VSCODE32SYSTEM
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{F8A2A208-72B3-4D61-95FC-8A65D340689B}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

  // VSCODE64SYSTEM
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{EA457B21-F73E-494C-ACAB-524FDE069978}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
  vscode_path: string;
  mbr: Integer;
  ErrCode: Integer;
begin
  LOG('id:'+inttostr(CurPageID));

  if (CurPageID<>wpWelcome) then
  begin
    result:=true;
    exit;
  end;

  vscode_path:=GetVSCodeInstallPath();
  LOG(vscode_path);

  if Length(vscode_path)=0 then
  begin
    mbr := MsgBox('This application requires VSCode. Please install the VSCode then run this installer again.', mbInformation, MB_OKCANCEL);
    if (mbr= IDOK) then
      ShellExec('open', 'https://code.visualstudio.com/Download', '', '', SW_SHOW, ewNoWait, ErrCode);
    exit;
  end;

  result:=true;
end;

procedure InitializeWizard;
begin
  WizardForm.Color := clWhite;
end;